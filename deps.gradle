def mavenPlugin = "com.github.dcendents:android-maven-plugin:1.2"
def androidPlugin = "com.android.tools.build:gradle:1.2.3"
def printVersions = false

def gradleVersions = gradle.gradleVersion.split('\\.')
def gradleMajorVersion = gradleVersions[0].toInteger()
def gradleMinorVersion = gradleVersions[1].toInteger()
def gradleMaintenanceVersion = 0
if (gradleVersions.length >= 3) {
  gradleMaintenanceVersion = gradleVersions[2].toInteger()
}

// Setup Maven Plugin
if (gradleMajorVersion == 4) {
  if (gradleMinorVersion < 4) {
    mavenPlugin = "com.github.dcendents:android-maven-gradle-plugin:1.5"
  } else if (gradleMinorVersion < 6) {
    mavenPlugin = "com.github.dcendents:android-maven-gradle-plugin:2.0"
  } else {
    mavenPlugin = "com.github.dcendents:android-maven-gradle-plugin:2.1"
  }
}

// Setup Android Plugin
if (gradleMajorVersion == 4) {
  if (gradleMinorVersion < 4) {
    androidPlugin = "com.android.tools.build:gradle:3.0.0"
  } else if (gradleMinorVersion < 6 ) {
    androidPlugin = "com.android.tools.build:gradle:3.1.0"
  } else {
    androidPlugin = "com.android.tools.build:gradle:3.2.1"
  }
}

ext.mavenPlugin = mavenPlugin
ext.androidPlugin = androidPlugin

ext {
  kotlin_version = "1.3.11"
  kotlinPlugin = "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  sonarqubeVersion = "2.6.1"
  sonarqubePlugin = "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:$sonarqubeVersion"
}

if (gradleMajorVersion < 4 || (gradleMajorVersion == 4 && gradleMinorVersion < 8)) {
  println "WARNING: Using Gradle version < 4.8 is unsupported"
}

if (printVersions) {
  println "gradleMajorVersion = " + gradleMajorVersion
  println "gradleMinorVersion = " + gradleMinorVersion
  println "gradleMaintenanceVersion = " + gradleMaintenanceVersion
  println "androidPlugin = " + androidPlugin
  println "mavenPlugin = " + mavenPlugin
  println "kotlinPlugin = " + kotlinPlugin
  println "sonarqubePlugin = " + sonarqubePlugin
}
