/**
 * Copyright (C) 2016 Clover Network, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
group = 'com.clover.sdk'
version = '262.2'


apply from: file("${project.rootDir}/lib.gradle")

dependencies {
  implementation project(':clover-android-sdk')
  implementation "androidx.annotation:annotation:$ANDROIDX_ANNOTATION_VERSION"
}

apply plugin: 'signing'

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
  }
}
apply plugin: 'io.codearte.nexus-staging'

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "${upload_release_url}") {
        authentication(userName: "${upload_user}", password: "${upload_password}")
      }
      snapshotRepository(url: "${upload_snapshot_url}") {
        authentication(userName: "${upload_user}", password: "${upload_password}")
      }

      pom.project {
        parent {
          groupId 'org.sonatype.oss'
          artifactId 'oss-parent'
          version '7'
        }

        name 'Clover Android Loyalty Kit'
        description 'Android services and objects used for interacting with Clover Loyalty Kit'
        url 'http://www.clover.com/'

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0'
            distribution 'repo'
          }
        }

        scm {
          url "https://github.com/clover/clover-android-sdk"
          connection "https://github.com/clover/clover-android-sdk.git"
          developerConnection "git@github.com:clover/clover-android-sdk.git"
        }

        developers {
          developer {
            name 'Clover Engineering Team'
            url 'https://www.clover.com/team'
          }
        }
      }
    }
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

artifacts {
  archives sourcesJar
}

signing {
  required = { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

nexusStaging {
  packageGroup = "com.clover" //optional if packageGroup == project.getGroup() ------>  NOTE: project.getGroup() returns com.clover.sdk
}
